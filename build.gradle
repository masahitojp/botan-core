apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

group = 'com.github.masahitojp'
archivesBaseName = "botan"
version = '0.0.0.8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile)*.options*.encoding = defaultEncoding
tasks.withType(GroovyCompile)*.groovyOptions*.encoding = defaultEncoding

def srcJava = 'src/main/java'
def srcDelomboked = 'build/src-delomboked/main/java'

repositories {
    mavenCentral()
}
configurations {
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

dependencies {
    provided "org.projectlombok:lombok:1.16.+"
    compile "org.igniterealtime.smack:smack-java7:4.1.3"
    compile "org.igniterealtime.smack:smack-tcp:4.1.3"
    compile "org.igniterealtime.smack:smack-im:4.1.3"
    compile "org.igniterealtime.smack:smack-extensions:4.1.3"
    compile "org.reflections:reflections:0.9.10"
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:java-hamcrest:2.0.0.0"
}


jar.manifest {
    attributes(
            'Implementation-Title' : name,
            'Implementation-Version': version,
            'Implementation-Vendor': 'com.github.masahitojp',
            'Built-JDK': "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})",
            'Built-Gradle': gradle.gradleVersion
    )
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar) {

    classifier = "javadoc"
    from javadoc
}
artifacts {
    archives javadocJar, sourcesJar
}


signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'botan'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'It is determined whether or not the haiku'
                url 'https://github.com/masahitojp/botan'

                scm {
                    url 'https://github.com/masahitojp/botan.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'masahito'
                        name "Nakamura Masato"
                        email "randomstep@gmail.com"
                    }
                }
            }
        }
    }
}


task delombok {
    inputs.files file(srcJava)
    outputs.dir file(srcDelomboked)

    doLast {
        FileCollection collection = files(configurations.compile)
        FileCollection sumTree = collection + fileTree(dir: 'bin')

        ant.taskdef(name: 'delombok', classname: 'lombok.delombok.ant.Tasks$Delombok', classpath: configurations.provided.asPath)
        ant.delombok(from:srcJava, to:srcDelomboked, classpath: sumTree.asPath)
    }
}

javadoc {
    dependsOn delombok
    source = srcDelomboked
    classpath = sourceSets.main.compileClasspath
    options {
        links "https://docs.oracle.com/javase/8/docs/api/"
        links "http://projectlombok.org/api/"
    }
}